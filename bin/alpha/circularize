#!/usr/bin/env perl

use strict;
use warnings;
use 5.012;

use BioX::Seq::Stream;
use Cwd qw/getcwd abs_path/;
use File::Temp;
use FindBin;

#my $fn_minimus2 = abs_path( $ARGV[0] );
my $fn_minimus2 = abs_path( "$FindBin::Bin/minimus2_fixed/" );

my $max_split_size = 500;

# if second argument is defined, it will read from that;
# otherwise, reads from STDIN by default
my $p = BioX::Seq::Stream->new( $ARGV[0] );;
my %seqs;
while (my $seq = $p->next_seq) {

    $seqs{$seq->id} = {
        seq => $seq,
        len => length($seq),
        is_circular => 0,
    }

}

for my $chr (sort {
    $seqs{$b}->{len} <=> $seqs{$a}->{len}
} keys %seqs) {
    print circularize(
        $seqs{$chr}->{seq}
    )->as_fasta;
}

sub circularize {

    my ($seq) = @_;
    my $wd = File::Temp->newdir(CLEANUP => 1);

    my $cwd = abs_path( getcwd());
    chdir $wd;

    my $id = $seq->id;
    my $desc = $seq->desc;

    my $l = length($seq);
    my $split_size = $l/2 < $max_split_size
        ? int($l/2)
        : $max_split_size;

    open my $split, '>', "split.fa";
    print {$split} $seq->range(1 => $split_size)->as_fasta;
    print {$split} $seq->range($split_size+1 => $l)->as_fasta;
    close $split;

    my $ret = system( "toAmos -s split.fa -o split.afg > /dev/null" );
    die "toAmos failed: $!" if ($ret);

    # Try to run minimus2 with increasingly lower errors until it succeeds.
    # This is to deal with the issue of minimus2 crashing with some input seqs
    for my $err (0.2,0.1,0.05) {
        my $ret = system( "runAmos -C $fn_minimus2 split -D CONSERR=$err > /dev/null");
        last if (! $ret);
    }

    # run succeeded
    if (-e 'split.fasta') {

        # was circularized
        if (-s 'split.fasta') {
            my @seqs;
            my $p = BioX::Seq::Stream->new('split.fasta');
            while (my $s = $p->next_seq) {
                push @seqs, $s;
            }
            if (scalar(@seqs) == 1 && (! -s 'split.singletons')) {
                my $id = $seq->id;
                $seq = $seqs[0];
                $seq->id = $id;
                $seq->desc = defined $desc
                    ? $desc . ' circular=true'
                    : 'circular=true';
                say STDERR "Circularized $id";
            }
            else {
                warn "Unexpected number of output contigs or singletons for $id\n";
            }

        }
        else {
            say STDERR "No circularization for $id";
        }

    }

    else {
        warn "Minimus2 failed completely on $id, returning original sequence\n";
    }

    chdir $cwd;

    return $seq;

}

