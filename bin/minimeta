#!/usr/bin/env perl

use strict;
use warnings;
use 5.012;
use autodie;

use BioX::Seq::Fetch;
use BioX::Seq::Stream;
use Cwd qw/abs_path getcwd/;
use File::Basename qw/basename/;
use File::Copy qw/copy/;
use File::Temp;
use Getopt::Long;
use IPC::Cmd qw/can_run run run_forked/;
use List::Util qw/min/;
use Pod::Usage;
use Time::Seconds;

our $VERSION = 0.010;
use constant PROGRAM => 'minimeta';

#-inputs---------------------------------------------------------------------#
my $fi_reads;
my $fi_consensus;
my $fi_homopolish;
#-outputs--------------------------------------------------------------------#
my $fo_consensus;
#-knobs----------------------------------------------------------------------#
my $threads  = 1;
my $n_racon  = 0;
my $n_medaka = 1;
my $quiet    = 0;
my $min_cov  = 2;
my $min_len  = 1;
my $hp_model = 'R9.4.pkl';
my $med_model;
my $mask_below;
my $split;
my $only_split_at_hp = 0;
my $minimizer_cutoff;
my $reduce = 0;
my $min_ident = 0.8;
my $shuffle = 1;
my $deterministic = 0;
my $trim_polyN = 0;
my $polyN_len = 30;
my $chunk_size;
my $reassemblies = 0;
my $medaka_batch_size = 100;

GetOptions(
    #-inputs-----------------------------------------------------------------#
    'in=s'         => \$fi_reads,
    'assembly=s'   => \$fi_consensus,
    'homopolish=s' => \$fi_homopolish,
    #-outputs----------------------------------------------------------------#
    'out=s' => \$fo_consensus,
    #-knobs------------------------------------------------------------------#
    'threads=i'           => \$threads,
    'n_racon=i'           => \$n_racon,
    'n_medaka=i'          => \$n_medaka,
    'min_cov=i'           => \$min_cov,
    'min_len=i'           => \$min_len,
    'mask_below=f'        => \$mask_below,
    'split!'              => \$split,
    'hp_model=s'          => \$hp_model,
    'medaka_model=s'      => \$med_model,
    'only_split_at_hp'    => \$only_split_at_hp,
    'minimizer_cutoff=i'  => \$minimizer_cutoff,
    'reduce'              => \$reduce,
    'reassemblies=i'      => \$reassemblies,
    'chunk_size=i'        => \$chunk_size,
    'min_ident=f'         => \$min_ident,
    'shuffle!'            => \$shuffle,
    'trim_polyN!'         => \$trim_polyN,
    'medaka_batch_size=i' => \$medaka_batch_size,
    'deterministic'       => \$deterministic,
    'quiet'               => \$quiet,
    'help'                => sub{ pod2usage(-verbose => 2, -exitval => 0); },
    'version'             => sub{ say 'This is ',PROGRAM,' v', $VERSION; exit },
) or pod2usage( -verbose  => 1);

# check for required software
my $MINIMAP2 = can_run('minimap2')
    // die "Minimap2 is required but not found\n";

my $SEQKIT = can_run('seqkit');
if ($shuffle && ! defined $SEQKIT) {
    die "SeqKit is required for shuffling but not found\n";
}

my $CUTADAPT = can_run('cutadapt');
if ($trim_polyN && ! defined $CUTADAPT) {
    die "Cutadapt is required for trimming but not found\n";
}

my $MINIASM = can_run('miniasm');
if (! defined $fi_consensus && ! defined $MINIASM) {
    die "Miniasm is required but not found\n";
}

my $RACON = can_run('racon');
if ($n_racon > 0 && ! defined $RACON) {
    die "Racon is required for polishing but not found\n";
}

my $MEDAKA = can_run('medaka_consensus');
if ($n_medaka > 0 && ! defined $MEDAKA) {
    die "Medaka is required for polishing but not found\n";
}

my $SAMTOOLS = can_run('samtools');
if (defined $mask_below && ! defined $SAMTOOLS) {
    die "Samtools is required for masking/splitting but not found\n";
}

my $BEDTOOLS = can_run('bedtools');
if (defined $mask_below && ! defined $BEDTOOLS) {
    die "Bedtools is required for masking/splitting but not found\n";
}

my $HOMOPOLISH = can_run('homopolish');
if (defined $fi_homopolish && ! defined $HOMOPOLISH) {
    die "Homopolish is required for polishing but not found\n";
}

my $REDUNDANS = can_run('redundans.py');
if ($reduce && ! defined $REDUNDANS) {
    die "Redundans is required for reducing but not found\n";
}


# globals

die "Must define existing input file (--in)\n"
    if (! defined $fi_reads || ! -r $fi_reads);
$fi_reads = abs_path($fi_reads);

if (defined $fi_consensus) {
    die "Input assembly not found or readable\n"
        if (! -r $fi_consensus);
    $fi_consensus = abs_path($fi_consensus);
}

if (defined $fi_homopolish) {
    die "Homopolish reference not found or readable\n"
        if (! -r $fi_homopolish);
    $fi_homopolish = abs_path($fi_homopolish);
}

if (defined $fo_consensus) {
    $fo_consensus = abs_path($fo_consensus);
}

# work in scratch space
my $init_dir = abs_path(getcwd());
my $in_base = basename($fi_reads);
my $wd =  File::Temp->newdir(CLEANUP => 1);
chdir $wd;

# Preprocess reads, as asked
if ($shuffle) {
    $in_base = shuffle( $fi_reads );
}
else {
    symlink $fi_reads, $in_base;
}
if ($trim_polyN) {
    $in_base = trim_polyN( $in_base );
}

# $in_base now points to (processed) reads
my $curr_cons = File::Temp->new();
close $curr_cons;

# initial assembly
if (defined $fi_consensus) {
    copy $fi_consensus, "$curr_cons";
    plog( 'assembly', "Using provided assembly" );
}
else {
    $curr_cons = defined $chunk_size
        ? assemble_chunked( $in_base )
        : assemble( $in_base );
}

check_empty($curr_cons);

# perform secondary assemblies if requested
for my $i (1..$reassemblies) {
    my $pseudoreads = generate_pseudoreads($curr_cons);
    $curr_cons = assemble( $pseudoreads, 're-assembly' );
}

check_empty($curr_cons);

# racon polishing
for my $i (1..$n_racon) {
    $curr_cons = racon( $in_base, $curr_cons );
}

# homopolishing
if (defined $fi_homopolish) {
    $curr_cons = homopolish( $fi_homopolish, $curr_cons );
}

# medaka polishing
for my $i (1..$n_medaka) {
    $curr_cons = medaka( $in_base, $curr_cons );
}

# coverage masking/splitting
if (defined $mask_below) {
    $curr_cons = mask_below(
        $in_base,
        $curr_cons,
        $mask_below,
        $split
    );
}

# Redundans reduction
if ($reduce) {
    $curr_cons = redundans( $curr_cons, $min_ident );
}

# always run this, and run it last, since we rename the contigs as well
$curr_cons = filter_short( $curr_cons, $min_len );

# log final statistics
summarize_assembly($curr_cons);

# write to final destination
open my $in, '<', $curr_cons;
my $fh_out = \*STDOUT;
if (defined $fo_consensus) {
    open my $fh, '>', $fo_consensus;
    $fh_out = $fh;
}
while (my $line = <$in>) {
    print {$fh_out} $line;
}

chdir $init_dir;
exit;

sub assemble {

    my ($fi_reads, $log) = @_;

    $log //= 'assembly';
    plog( $log, ucfirst( "$log with miniasm" ) );
    my $ts = time;

    my $paf = map_ava( $fi_reads );
    my $cons = miniasm( $fi_reads, $paf );

    my $n = n_seqs($cons);
    my $el = Time::Seconds->new(time - $ts)->pretty;
    plog( $log, ucfirst( "$log took $el" ) );
    plog( $log, "Assembled $n contigs" );

    return $cons;

}

sub assemble_chunked {

    my ($fi_reads) = @_;

    my $p = BioX::Seq::Stream->new($fi_reads);
    my $curr_chunk = File::Temp->new();
    my $curr_n = 0;
    my @assemblies;
    my $i = 1;
    while (my $seq = $p->next_seq) {
        print {$curr_chunk} $seq->as_input;
        ++$curr_n;
        if ($curr_n >= $chunk_size) {
            close $curr_chunk;
            push @assemblies, assemble( $curr_chunk, 'sub-assembly' );
            ++$i;
            $curr_n = 0;
            $curr_chunk = File::Temp->new();
        }
    }
    # process last chunk
    if ($curr_n >= 1) {
        close $curr_chunk;
        push @assemblies, assemble( $curr_chunk, 'sub-assembly' );
    }

    # combine assemblies into single FASTA
    my $tmp_combined = File::Temp->new();
    my $ci = 0;
    for my $ass (@assemblies) {
        my $p = BioX::Seq::Stream->new("$ass");
        while (my $seq = $p->next_seq) {
            $seq->id = sprintf("c%s", $ci++);
            print {$tmp_combined} $seq->as_fasta;
        }
    }
    close $tmp_combined;

    # shred contigs into pseudo-reads
    my $pseudoreads = generate_pseudoreads( $tmp_combined );
    my $min_cov_orig = $min_cov;
    $min_cov = 1;
    my $cons = assemble( $pseudoreads, 're-assembly' );
    $min_cov = $min_cov_orig;

    return $cons;

}

sub check_empty {
    
    my ($fi) = @_;

    return if (-s "$fi");

    plog( 'warning', "Assembly empty, skipping polishing" );
    if (defined $fo_consensus) {
        copy "$fi", $fo_consensus;
    }
    chdir $init_dir;
    exit;

}

sub map_ava {
    
    my ($fi) = @_;

    my $tmp_out = File::Temp->new();
    close $tmp_out;

    my @fmax = ();
    if (defined $minimizer_cutoff) {
        push @fmax, '-f', $minimizer_cutoff;
    }
    my @cmd = (
        $MINIMAP2,
        '-t' => $threads,
        '-x' => 'ava-ont',
        '-o' => "$tmp_out",
        @fmax,
        "$fi",
        "$fi",
    );
    my $res = run_forked( \@cmd );
    if ($res->{exit_code} != 0) {
        die "Initial AVA mapping failed: $res->{err_msg}";
    }

    return $tmp_out;
}

sub mask_below {
    
    my ($fi_reads, $fi_cons, $mask_below, $split) = @_;

    my ($v1, $v2) = defined $split
        ? ('split', 'Splitting')
        : ('mask', 'Masking');
    plog( $v1, "$v2 at regions with coverage below $mask_below" );

    my $ts = time;
    # map reads against existing consensus
    # minimap2 -ax map-ont trim.fa trimmed.fq > rc.sam
    my @cmd_mm = (
        $MINIMAP2,
        '-t' => $threads,
        '-a',
        '-x' => 'map-ont',
        '-o' => 'mask.sam',
        $fi_cons,
        $fi_reads,
    );
    run( command => \@cmd_mm )
        or die "consensus read mapping failed: $!";

    my @cmd_sort = (
        $SAMTOOLS,
        'sort',
        '--threads' => $threads,
        '-o' => 'sorted.bam',
        'mask.sam',
    );
    run( command => \@cmd_sort )
        or die "SAM sorting failed: $!";

    my @cmd_cov = (
        $BEDTOOLS,
        'genomecov',
        '-ibam' => 'sorted.bam',
        #'-split',
        '-bga',
    );
    my ( $success, $error_message, $full_buf, $stdout_buf, $stderr_buf )
        = run( command => \@cmd_cov );
    die "Error running bedtools genomecov: $error_message"
        if (! $success);
    open my $bg, '>', 'coverage.bedgraph';
    print {$bg} buff_to_str($stdout_buf);
    close $bg;

    # fractional cutoff is interpreted as fraction of median depth,
    # so we need to calculate that here for each contig
    my $individual_cutoffs;
    if ($mask_below < 1 && $mask_below > 0) {

        # create contig interval BED, using sort order from bedgraph
        open my $chr_bed, '>', 'chrs.bed';
        my @order;
        open my $cov, '<', 'coverage.bedgraph';
        while (my $line = <$cov>) {
            chomp $line;
            my ($chr, @other) = split "\t", $line;
            push @order, $chr
            if (! scalar(@order) || $chr ne $order[-1]);
        }
        close $cov;
        
        my $p = BioX::Seq::Fetch->new($fi_cons);
        for my $chr (@order) {
            my $seq = $p->fetch_seq($chr)
                // die "Seq $chr missing in input";
            say {$chr_bed} join "\t",
                $seq->id,
                0,
                length($seq),
            ;
        }
        close $chr_bed;

        my @cmd_map = (
            $BEDTOOLS,
            'map',
            '-a' => 'chrs.bed',
            '-b' => 'coverage.bedgraph',
            '-c' => '4',
            '-o' => 'median',
        );
        my ( $success, $error_message, $full_buf, $stdout_buf, $stderr_buf )
            = run( command => \@cmd_map );
        die "Error running bedtools map: $error_message"
            if (! $success);
        # this hack is necessary to process output by line; otherwise buffering can
        # cause incomplete lines to be processed
        my $stdout = join '', @{ $stdout_buf};
        open my $stream, '<', \$stdout;
        while (my $line = <$stream>) {
            chomp $line;
            my ($chr, $s, $e, $depth) = split "\t", $line;
            $individual_cutoffs->{$chr} = $depth * $mask_below;
        }
        close $stream;

    }

    my %split_points;
    my $mask;
    if (! $split) {
        open $mask, '>', 'mask.bed';
    }
    open my $stream, '<', 'coverage.bedgraph';
    while (my $line = <$stream>) {
        chomp $line;
        my @fields = split "\t", $line;
        my $cutoff = defined $individual_cutoffs
            ? ($individual_cutoffs->{ $fields[0] } // 0)
            : $mask_below;
        next if ($fields[3] >= $cutoff);
        if ($split) {
            push @{ $split_points{$fields[0]} }, [$fields[1]+1, $fields[2]];
        }
        else {
            say {$mask} $line;
        }
    }
    close $mask
    	if (defined $mask);
    close $stream;

    my $tmp_out = File::Temp->new();

    # Here we perform masking only
    if (! $split) {

        close $tmp_out;

        # mask consensus
        my @cmd_mask = (
            $BEDTOOLS,
            'maskfasta',
            '-fi' => $fi_cons,
            '-bed' => 'mask.bed',
            '-fo' => "$tmp_out",
        );
        run( command => \@cmd_mask )
            or die "Error running bedtools maskfasta: $!";

    }

    # ... or we split the contigs
    else {

        my $p = BioX::Seq::Stream->new($fi_cons);
        CHR:
        while (my $seq = $p->next_seq) {

            my $chr = $seq->id;
            my @points = defined $split_points{$chr}
                ? @{ $split_points{$chr} }
                : ();
            if ($only_split_at_hp) {
                my @new;
                my $win = 5;
                for my $p (@points) {
                    my $hp = 0;
                    for (@$p) {
                        my $l = $_ > $win ? $_-$win : 1;
                        my $r = length($seq) - $_ > $win ? $_+$win : length($seq);
                        my $s = $seq->range($l, $r);
                        ++$hp if ($s->seq =~ /(\w)(\1{3,})/);
                    }
                    push @new, $p if ($hp > 0);
                }
                @points = @new;
            }
            if (! scalar @points) {
                print {$tmp_out} $seq->as_fasta;
                next CHR;
            }
            my $i = 1;
            for my $p (@points) {
                if ($p->[0] >= $i) {
                    my $sub = $seq->range($i, $p->[0]-1);
                    my $l = length $sub;
                    if ($l > 0) {
                        $sub->desc //= "LN:i:$l";
                        $sub->desc =~ s/\bLN:i:\d+\b/LN:i:$l/;
                        print {$tmp_out} $sub->as_fasta;
                    }
                }
                $i = $p->[1] + 1;
            }
            if ($i <= length($seq)) {
                my $sub = $seq->range($i, length($seq));
                my $l = length $sub;
                if ($l > 0) {
                    $sub->desc =~ s/\bLN:i:\d+\b/LN:i:$l/;
                    print {$tmp_out} $sub->as_fasta;
                }
            }

        }
                
        close $tmp_out;
    }

    my $n_start = n_seqs($fi_cons);
    my $n_end = n_seqs($tmp_out);
    my $n_splits = $n_end - $n_start;
    my $el = Time::Seconds->new(time - $ts)->pretty;
    plog( $v1, "$v2 took $el" );
    if (defined $split) {
        plog( 'split', "$n_splits splits made" );
    }

    return $tmp_out;

}
    

sub miniasm {
    
    my ($fi_reads, $fi_paf) = @_;

    my @cmd = (
        $MINIASM,
        '-s' => '200',
        '-c' => $min_cov,
        '-e' => 1,
        '-I' => '0.2',
        '-f' => "$fi_reads",
        $fi_paf,
    );
    #open my $stream, '-|', @cmd;
    my ( $success, $error_message, $full_buf, $stdout_buf, $stderr_buf )
        = run( command => \@cmd );
    die "Error running miniasm: $error_message"
        if (! $success);

    # convert GFA to FASTA
    my $tmp_out = File::Temp->new();
    # this hack is necessary to process output by line; otherwise buffering can
    # cause incomplete lines to be processed
    my $stdout = join '', @{ $stdout_buf};
    open my $stream, '<', \$stdout;
    while (my $line = <$stream>) {
        chomp $line;
        my @fields = split "\t", $line;
        next if ($fields[0] ne 'S');
        print {$tmp_out} BioX::Seq->new(
            $fields[2],
            $fields[1],
            $fields[3],
        )->as_fasta;
    }
    close $tmp_out;
    close $stream;

    return $tmp_out;

}

sub racon {
    
    my ($fi_reads, $fi_cons) = @_;

    plog( 'racon', "Polishing with racon" );
    my $ts = time;

    # map reads against existing consensus
    # minimap2 -ax map-ont trim.fa trimmed.fq > rc.sam
    my @cmd_mm = (
        $MINIMAP2,
        '-t' => $threads,
        '-a',
        '-x' => 'map-ont',
        '-o' => 'rc.sam',
        $fi_cons,
        $fi_reads,
    );
    run( command => \@cmd_mm )
        or die "consensus read mapping failed: $!";

    # call new consensus with Racon
    # racon -m 8 -x -6 -g -8 -w 500 --no-trimming trimmed.fq rc.sam trim.fa > rc.fa
    my @cmd_rc = (
        $RACON,
        '--threads'       => $threads,
        '--match'         => 8,
        '--mismatch'      => '-6',
        '--gap'           => '-8',
        '--window-length' => 500,
        '--no-trimming',
        $fi_reads,
        'rc.sam',
        $fi_cons
    );
    
    my ( $success, $error_message, $full_buf, $stdout_buf, $stderr_buf )
        = run( command => \@cmd_rc );
    die "Error running racon: $error_message"
        if (! $success);

    my $tmp_out = File::Temp->new();
    # this hack is necessary to process output by line; otherwise buffering can
    # cause incomplete lines to be processed
    my $stdout = join '', @{ $stdout_buf};
    open my $stream, '<', \$stdout;
    while (my $line = <$stream>) {
        print {$tmp_out} $line;
    }
    close $tmp_out;
    close $stream;

    my $el = Time::Seconds->new(time - $ts)->pretty;
    plog( 'racon', "Round took $el"  );

    return $tmp_out;

}

sub medaka {

    my ($fi_reads, $fi_cons) = @_;

    plog( 'medaka', "Polishing with medaka" );
    my $ts = time;

    my @cmd = (
        $MEDAKA,
        '-t' => $threads,
        '-f',
        '-b' => $medaka_batch_size,
        '-i' => $fi_reads,
        '-d' => $fi_cons,
        '-o' => 'polished',
    );
    if (defined $med_model) {
        push @cmd, '-m' => $med_model;
    }
    run( command => \@cmd )
        or die "Call to medaka failed: $!";

    # transfer annotations dropped by Medaka
    my $cons = transfer_annots(
        $fi_cons,
        'polished/consensus.fasta',
    );

    my $el = Time::Seconds->new(time - $ts)->pretty;
    plog( 'medaka', "Round took $el"  );

    return $cons;

}

sub transfer_annots {

    my ($fn_old, $fn_new, $strip_suffix) = @_;

    my $tmp_out = File::Temp->new();

    # read annotations from input contigs
    my %annots;
    my $p = BioX::Seq::Stream->new($fn_old);
    while (my $seq = $p->next_seq) {
        $annots{$seq->id} = $seq->desc // '';
    }
    # copy annotations to output contigs
    $p = BioX::Seq::Stream->new($fn_new);
    while (my $seq = $p->next_seq) {
        my $id = $seq->id;
        if (defined $strip_suffix) {
            $id =~ s/$strip_suffix$//;
            $seq->id = $id;
        }
        my $tags = $annots{$id}
            // die "Missing annotations for contig " . $id;
        my $l = length $seq;
        $tags =~ s/\bLN:i:\d+\b/LN:i:$l/;
        $seq->desc = join ' ',
            grep {defined $_ && length $_} $seq->desc, $tags;
        print {$tmp_out} $seq->as_fasta;
    }

    close $tmp_out;
    return $tmp_out;

}

sub shuffle {
    
    my ($fi_reads) = @_;

    plog( 'shuffle', 'Shuffle input reads' );
    my $ts = time;

    my $tmp_out = File::Temp->new();
    close $tmp_out;

    my $seed = $deterministic ? 22 : int(rand(999999));
    my @cmd = (
        $SEQKIT,
        'shuffle',
        '-s' => $seed,
        '-o' => "$tmp_out",
        "$fi_reads",
    );
    run( command => \@cmd )
        or die "shuffle failed: $!";

    my $el = Time::Seconds->new(time - $ts)->pretty;
    plog( 'shuffle', "Shuffle took $el" );

    return $tmp_out;

}

sub trim_polyN {
    
    my ($fi_reads) = @_;

    plog( 'trim', 'Trim polyN stretches' );
    my $ts = time;

    my $tmp_out = File::Temp->new();
    close $tmp_out;

    my @cmd = (
        $CUTADAPT,
        '-b' => "A{$polyN_len}",
        '-b' => "T{$polyN_len}",
        '-b' => "G{$polyN_len}",
        '-b' => "C{$polyN_len}",
        '--times' => '50',
        '--minimum-length' => '100',
        '--report' => 'minimal',
        '-o' => "$tmp_out",
        $fi_reads,
    );
    my ( $success, $error_message, $full_buf, $stdout_buf, $stderr_buf )
        = run( command => \@cmd );
    die "Error running cutadapt: $error_message"
        if (! $success);
    my $so = buff_to_str($stdout_buf);
    my ($k, $v, @other) = split /\n/, $so;
    my %report;
    @report{ split "\t", $k } = split "\t", $v;
    for my $field (
        'in_reads',
        'out_reads',
        'in_bp',
        'out_bp',
        'w/adapters'
    ) {
        die "Missing field $field in cutadapt report\n"
            if (! defined $report{$field});
    }

    my $el = Time::Seconds->new(time - $ts)->pretty;
    plog( 'trim', "Trimming took $el" );
    plog( 'trim', sprintf(
        "Trimmed %0.1f%% of reads; Discarded %0.1f%% reads; Discarded %0.1f%% bases",
        $report{'w/adapters'} / $report{in_reads} * 100,
        ($report{in_reads} - $report{out_reads})/$report{in_reads}*100,
        ($report{in_bp} - $report{out_bp})/$report{out_bp}*100,
    ) );

    return $tmp_out;

}

sub filter_short {
    
    my ($fi_cons, $min_len) = @_;

    plog( 'filter', "Filtering contigs shorter than $min_len" );

    my $ts = time;
    my $n_start = n_seqs($fi_cons);
    
    my $p = BioX::Seq::Stream->new($fi_cons);
    my @final;
    while (my $seq = $p->next_seq) {
        next if (length $seq < $min_len);
        push @final, $seq;
    }

    my $tmp_out = File::Temp->new();

    my $padding = length (scalar @final - 1);
    my $i = 0; 
    for my $ctg (sort {length($b) <=> length($a)} @final) {
        $ctg->id = sprintf "ctg_%0${padding}s", $i++;
        print {$tmp_out} $ctg->as_fasta;
    }
    close $tmp_out;

    my $n_end = n_seqs($tmp_out);
    my $n_filt = $n_start - $n_end;
    my $el = Time::Seconds->new(time - $ts)->pretty;
    plog( 'filter', "Filtering took $el" );
    plog( 'filter', "$n_filt contigs removed" );

    return $tmp_out;

}

sub homopolish {
    
    my ($fi_ref, $fi_cons, $fo_cons) = @_;

    plog( 'homopolish', "Polishing with homopolish" );
    my $ts = time;

    my @cmd_hp = (
        $HOMOPOLISH,
        'polish',
        '-a' => $fi_cons,
        '-l' => $fi_ref,
        '-m' => $hp_model,
        '--minimap_args' => 'map-ont',
        '-o' => 'hp',
    );
    run( command => \@cmd_hp )
        or die "homopolish failed: $!";

    my $basename = basename($fi_cons);
    $basename =~ s/\..*//;
    my $hp_out = "hp/${basename}_homopolished.fasta";
    die "Missing homopolish output file\n"
        if (! -e $hp_out);

    # transfer annotations dropped by homopolish
    my $cons = transfer_annots(
        $fi_cons,
        $hp_out,
        '_polish',
    );

    my $el = Time::Seconds->new(time - $ts)->pretty;
    plog( 'homopolish', "Took $el" );

    return $cons;

}

sub redundans {

    my ($fi_cons, $min_ident) = @_;

    plog( 'reduce', "Removing redundant contigs with identity above $min_ident" );

    my $ts = time;
    my $n_start = n_seqs($fi_cons);
    
    my @cmd = (
        $REDUNDANS,
        '-t' => $threads,
        '--noscaffolding',
        '--nogapclosing',
        '--identity' => $min_ident,
        '-f' => $fi_cons,
        '-o' => 'reduced',
    );
    run( command => \@cmd )
        or die "Call to redundans failed: $!";

    # transfer annotations dropped by redundans
    my $new_cons = transfer_annots(
        $fi_cons,
        'reduced/scaffolds.reduced.fa',
    );

    my $n_end = n_seqs($new_cons);
    my $n_filt = $n_start - $n_end;
    my $el = Time::Seconds->new(time - $ts)->pretty;
    plog( 'reduce', "Reduction took $el" );
    plog( 'reduce', "$n_filt contigs removed" );

    return $new_cons;

}

sub plog {

    my ($unit, $msg) = @_;
    return if ($quiet);
    say STDERR "[mm::$unit] $msg";

}

sub buff_to_str {

    my ($buff, $fn) = @_;
    return join '', @{ $buff };

}

sub n_seqs {

    my ($fn) = @_;
    return scalar BioX::Seq::Fetch->new("$fn")->ids;

}

sub summarize_assembly {

    my ($fn) = @_;

    my $len_total = 0;
    my $n_seqs = 0;
    my $max_len = 0;
    my $p = BioX::Seq::Stream->new($fn);
    while (my $seq = $p->next_seq) {
        my $l = length $seq;
    ++$n_seqs;
    $len_total += $l;
    $max_len = $l if ($l > $max_len);
    }
    plog(
        'final',
        sprintf(
            "Final assembly: %s contigs; %s total bp; %s bp longest contig",
            $n_seqs,
            $len_total,
            $max_len,
        )
    );

}

sub generate_pseudoreads {

    my ($fi) = @_;

    my $n_reads = 20;
    my $frac_size = 0.4;

    my $tmp_out = File::Temp->new();

    my $i = 0;
    my $p = BioX::Seq::Stream->new("$fi");
    while (my $seq = $p->next_seq) {
        my $l = length($seq);
        my $lsub = int( $l*$frac_size );
        my $start = $seq->range(1, $lsub);
        $start->id = sprintf("q%s", $i++);
        print {$tmp_out} $start->as_input;
        my $end = $seq->range($l-$lsub+1, $l);
        $end->id = sprintf("q%s", $i++);
        print {$tmp_out} $end->as_input;
        for (1..$n_reads) {
            my $s = int(rand($l-$lsub))+1;
            my $e = min($l, $s+$lsub-1 );
            my $sub = $seq->range($s, $e);
            $sub->id = sprintf("q%s", $i++);
            print {$tmp_out} $sub->as_input;
        }
    }

    close $tmp_out;
    return $tmp_out;

}

__END__

=head1 NAME

minimeta - assembler for long-read metagenomic/metatranscriptomic data sets

=head1 SYNOPSIS

minimeta --in <reads.fq> --out <consensus.fasta>

=head1 DESCRIPTION

Produces a polished consensus assembly from long-read sequencing data using
miniasm, racon, and medaka. Software settings are tuned for
metagenomic/metatranscriptomic assemblies of variable, sometimes low,
coverage.

=head1 PREREQUISITES

Requires the following non-core Perl libraries:

=over 1

=item BioX::Seq

=back

Additionally, the following external programs are required for one or more of
the optional processing modules (errors will be thrown for missing programs
only if that module is requested). All optional dependencies are available in
Bioconda.

=over 1

=item minimap2

=item miniasm

=item racon

=item medaka

=item samtools

=item bedtools

=item seqkit

=item redundans

=item cutadapt

=item homopolish

=back

=head1 OPTIONS

=head2 Input

=over 4

=item B<--in> I<filename>

Path to input reads in FASTx format (required)

=item B<--assembly> I<filename>

Path to existing assembly. If provided, assembly is skipped and only polishing
is performed (default: none).

=item B<--homopolish> I<filename>

Path to reference FASTA file used by homopolish. Providing this filename also
triggers polishing using homopolish (default: none).

=back

=head2 Output 

=over 4

=item B<--out> I<filename>

Path to write consensus sequence to (as FASTA) [default: STDOUT]

=back

=head2 Configuration 

=over 4

=item B<--min_cov> I<integer>

Minimum read coverage required by assembler to keep position (default: 2)

=item B<--min_len> I<integer>

Minimum contig length to keep (default: 1)

=item B<--mask_below> I<integer>

If given, final assembly positions with coverage depth below this value will
be hard masked with 'N' (default: off)

=item B<--split> I<float>

If given in conjunction with C<--mask_below>, splits contigs at masked regions
into smaller pieces. (default: off)

=item B<--only_split_at_hp>

If given in conjuction with C<--split>, only splits low coverage regions
if one or both junctions is at a homopolymer stretch
(default: off)

=item B<--threads> I<integer>

Number of processsing threads to use for mapping and polishing (default: 1)

=item B<--n_racon> I<integer>

Number of Racon polishing rounds to perform (default: 3)

=item B<--n_medaka> I<integer>

Number of Medaka polishing rounds to perform (default: 1)

=item B<--medaka_model> I<string>

Name of model to be used by medaka_consensus (based on basecalling model used
for data) (default: depends on medaka version)

=item B<--medaka_batch_size> I<integer>

Batch size (medaka_consensus parameter B<-b>) for medaka to use; using a
smaller value should reduce memory consumption (default: 100)

=item B<--hp_model> I<string>

Name of model to be used by homopolish. Has no effect if B<--homopolish> not
used. (default: R9.4.pkl)

=item B<--noshuffle>

Don't randomly shuffle input reads prior to assembly (default: shuffle)

=item B<--trim_polyN>

Trim long poly-N stretches from reads prior to assembly (default: off)

=item B<--reassemblies> I<integer>

Perform one or more rounds of pseudo-assembly in order to minimize redundancy.
For each round, the existing assembly is shredded into pseudoreads and
reassembled.

=item B<--chunk_size> I<integer>

If this option is given, input reads will be split into chunks of
B<--chunk_size> reads and each chunk will be assembled independently. The
resulting assemblies will be combined, shredded into pseudoreads, and
reassembled.

=item B<--deterministic>

Use a fixed seed for random processes such as shuffling (default: off)

=item B<--reduce>

Apply a reduction algorithm to the pre-final assembly to remove redundant
contigs (i.e. contigs mostly or completely overlapping with identity above a
cutoff specified by B<--min_ident>. Currently this is done using Redundans,
which is required to be installed. (default: off)

=item B<--min_ident> I<float>

Minimum identity (0 to 1) between contigs required to remove shorter contig during
redundancy reduction. (default: 0.8)

=item B<--minimizer_cutoff> I<integer>

During all-vs-all mapping, discard minimizers occurring above this frequency.
This is the B<-f> parameter to minimap2, and can be useful with high-coverage
input datasets that may otherwise consume very large amounts of memory and
time. A value between 1000 and 10,000 may be useful in these cases. (default:
off)

=item B<--quiet>

Don't write status messages to STDERR

=item B<--help>

Print usage description and exit

=item B<--version>

Print software version and exit

=back

=head1 CAVEATS AND BUGS

Please submit bug reports to the issue tracker in the distribution repository.

=head1 AUTHOR

Jeremy Volkening (jeremy.volkening@base2bio.com)

=head1 LICENSE AND COPYRIGHT

Copyright 2021-23 Jeremy Volkening

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.

=cut

