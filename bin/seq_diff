#!/usr/bin/env perl

use strict;
use warnings;
use 5.012;

use BioX::Seq::Stream;
use Getopt::Long;
use Pod::Usage;
use List::MoreUtils qw/uniq/;

my $NAME = 'seq_diff';
my $VERSION = '0.002';

my $print_len;

GetOptions(
    'print_len'        => \$print_len,
    'man'              => sub{ pod2usage(-verbose => 2); },
    'help'             => sub{ pod2usage(-verbose => 2); },
    'version'          => sub{ print "This is $NAME v$VERSION\n" and exit },
) or pod2usage(
    -msg => 'Syntax error: please check your command',
    -verbose => 1
);


my $aln = $ARGV[0];

my $s = BioX::Seq::Stream->new($aln);

my @seqs;
my @ids;
while (my $seq = $s->next_seq) {
    push @seqs, uc $seq->seq;
    push @ids,  $seq->id;
}
die "Too few sequences\n"
    if (scalar @seqs < 2);

if ($print_len) {
    my $l = length $seqs[0];
    say "# length: $l bp";
}

say join "\t",
    'start',
    'end',
    @ids,
;

my $len = length $seqs[0];
my $last_start;
my $last_end;
#my @last_1;
#my @last_2;
my @lasts;
for my $pos (0..$len-1) {
    my @bases = map {substr $_, $pos, 1} @seqs;
    my @u = uniq @bases;
    if (scalar @u == 1) {
        if (defined $last_start) {
           
            # reformat long diff strings
            for my $last (@lasts) {
                if (scalar @$last > 1) {
                    my $bps  = grep {$_ ne '-'} @$last;
                    my $gaps = grep {$_ eq '-'} @$last;
                    @$last = ("$gaps gaps, $bps nt");
                }
            }
            say join "\t",
                $last_start,
                $last_end,
                (map {$_->[0]} @lasts),
             ;
             $last_start = undef;
             $last_end   = undef;
             @lasts = ();
        }
    }
    else {
        $last_start //= $pos+1;
        $last_end = $pos+1;
        for (0..$#bases) {
            push @{ $lasts[$_] }, $bases[$_];
        }
    }
}

#handle any remaining diffs
if (defined $last_start) {
    
    # reformat long diff strings
    for my $last (@lasts) {
        if (scalar @$last > 1) {
            my $bps  = grep {$_ ne '-'} @$last;
            my $gaps = grep {$_ eq '-'} @$last;
            @$last = ("$gaps gaps, $bps nt");
        }
    }
    say join "\t",
        $last_start,
        $last_end,
        (map {$_->[0]} @lasts),
        ;
        $last_start = undef;
        $last_end   = undef;
        @lasts = ();
}


exit;

__END__

=head1 NAME

seq_diff - list differences between two sequences by position

=head1 SYNOPSIS

seq_diff < aln_file > aln_diff

=head1 DESCRIPTION

This is a "diff-like" program which takes an alignment of two sequences in
FASTA format on STDIN and reports the differences by base position. It is
intended to be used on similar sequences with only a few expected changes -
otherwise the list of differences might get rather long. 

=head1 PREREQUISITES

Requires the following non-core Perl libraries:

=over 1

=item * BioX::Seq

=back

=head1 OPTIONS

=over

=item B<--print_len>

Add an info line to output indicating alignment length

=item B<--out> I<filename>

Path to FASTQ output file. If not given, output will be written to STDOUT.

=item B<--scheme> I<scheme>

The binning scheme to use. Currently three presets are supported:

B<illumina> -- Puts quality scores into one of eight bins:

>= 40 : 40

35-39 : 37

30-34 : 33

25-29 : 27

20-24 : 22

10-19 : 15

2-9 : 6

0-1 : 0

B<gatk> -- Puts quality scores into one of four bins (these were deduced from
various Broad Institute documents but are not guaranteed to be the same as
those used by GATK):

>= 33 : 40

23-32 : 30

13-22 : 20

3-12 : 10

0-2 : 0

B<colord> -- Puts quality scores into one of four bins according to the CoLoRd
preprint (used for long read compression) -- note that CoLoRd uses dynamic
output values to keep the same same average quality, but here they are hard-coded):

>= 26 : 40

14-25 : 20

7-13 : 10

1-6 : 3

0 : 0

=item B<--help>

Display this usage page

=item B<--version>

Print version information

=back


=head1 CAVEATS AND BUGS

Please submit bug reports to the issue tracker in the distribution repository.

=head1 AUTHOR

Jeremy Volkening (jdv@base2bio.com)

=head1 LICENSE AND COPYRIGHT

Copyright 2014-18 Jeremy Volkening

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.

=cut
