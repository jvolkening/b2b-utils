#!/usr/bin/env perl

use strict;
use warnings;
use 5.012;

use File::Temp;
use IPC::Cmd qw/can_run/;
use Getopt::Long;

my $clipboard  = 'clipboard'; # 'clipboard' or 'primary'
my $database   = 'nr';
my $program    = 'blastn';

GetOptions(
    'clipboard=s' => \$clipboard,
    'database=s'  => \$database,
    'program=s'   => \$program,
);

# check for necessary binaries
can_run('xclip')
    // die "Missing required xclip binary\n";
can_run('sensible-browser')
    // die "Missing required sensible-browser binary\n";

# read in clipboard contents and check quickly that they look like either raw
# sequence or FASTA (not an exhaustive check)
my $query;
my @cmd = (
    'xclip',
    '-selection' => $clipboard,
    '-o' => '-'
);
open my $stream, '-|',  @cmd;
while (my $line = <$stream>) {
    chomp $line;
    # sanity check returned contents
    exit 2 if ($line !~ /^(>|([A-Z]*\z))/i);
    $query .= $line . '&#13;';
}
close $stream
    or die "Error fetching clipboard: $!";

# sanity check returned contents
exit 1 if (! length $query);

# insert sequence into dummy form and write to temporary file
# (tried data URL here to avoid temporary file, but it doesn't automatically
# load in some browsers. Also, it seems the temporary file must be kept around
# after exit because the system command sometimes returns right away)
my $form = File::Temp->new(
    UNLINK => 0,
    SUFFIX => '.html'
);
print {$form} form_contents() ;
close $form;

# finally, load dummy form in browser
exit system('sensible-browser', "$form");

sub form_contents {

    return <<"FORM";
<?xml version="1.0" encoding="utf-8"?>
<!DOCTYPE html>
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
<head>
    <title></title>
    <meta http-equiv="content-type" content="application/xhtml+xml; charset=utf-8" />
</head>
<body onload="document.blast.submit();">
<form name="blast" action="https://blast.ncbi.nlm.nih.gov/Blast.cgi" method="post">
   <input type="hidden" name="QUERY" value="$query" />
   <input type="hidden" name="DATABASE" value="$database" />
   <input type="hidden" name="CMD" value="request" />
   <input type="hidden" name="PROGRAM" value="$program" />
   <input type="hidden" name="MEGABLAST" value="on" />
   <input type="hidden" name="SHOW_OVERVIEW" value="on" />
</form>
</body>
</html>
FORM

}
